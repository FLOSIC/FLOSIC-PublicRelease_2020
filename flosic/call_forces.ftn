! UTEP Electronic Structure Lab (2020)
!
! *****************************************************
!
! APOTNL (DIRK POREZAG, 1994, 1998) (MARK PEDERSON 1987-1994)
! CODE ORIGINALLY FROM END OF APOTNL
! IF CONVERGENCE IS REACHED, CALCULATE FORCES
! IF NOT REACHED, DO POTENTIAL MIXING
!

       SUBROUTINE CALL_FORCES
       use global_inputs,only : EXCITED1, NONSCF1, MAXSCF,
     &     MIXING1
       use pot_dens,only : COULOMB,RHOG
       use mixpot1,only : POTIN,POT=>POTOUT
       use mesh1,only : nmsh
       use common2,only : NSPN
       use common5,only : CONVERGENCE
       use common9,only : old_mode
       use solvent,only : POTSOL
! Conversion to implicit none.  Raja Zope Sun Aug 20 09:01:47 MDT 2017

!      INCLUDE  'PARAMAS'  
       INCLUDE  'PARAMA2'  
       INTEGER :: IERR
       INTEGER :: IPTS, ITER, MPTS, MSPN, NSPTS
       REAL*8 :: AVG 
       SAVE

       LOGICAL AVERAGE
       CHARACTER*7 NAMES
       DIMENSION NAMES(3)
!
       DATA NAMES/'BROYDEN','KBROY1','KBROY2'/
       DATA AVERAGE/.FALSE./

!
! IF (CONVERGENCE) WRITE POTENTIAL WITH ITER=0 AND GET FORCES
!
       IF(CONVERGENCE)THEN
!
! IF CONVERGENCE THEN DEALLOCATE FOR WORKERS, BUT MANAGER KEEPS
!  RESULTS ALLOCATED
!
%ifdef MPI
        CALL SENDDATA(110)
%endif
        ITER=0
        OPEN(99,FILE='POTOLD',FORM='UNFORMATTED',STATUS='UNKNOWN')
        REWIND(99)
        WRITE(99)NMSH,NSPN,ITER
        WRITE(99)(POT(IPTS), IPTS=1,NMSH*NSPN)
        CLOSE(99)
        PRINT '(A)',' '
        PRINT '(A)','SELF-CONSISTENCY REACHED, CALCULATING FORCES'
        if (old_mode) call check_inputs
        IF(.NOT.EXCITED1)THEN
          CALL HFFLOCAL(RHOG(1,1,1))
          CALL FRCNONL
          CALL FPULAY
        ENDIF
        RETURN
       ELSE
!
! DEALLOCATE COULOMB AND RHOG HERE VIA A CALL TO SENDDATA
!
! CONVERGENCE WAS NOT MET SO WE WILL DEALLOCATE FOR NEXT ITERATION
!
%ifdef MPI
        CALL SENDDATA(109)
%else
        DEALLOCATE(COULOMB,STAT=IERR)
        IF(IERR.NE.0)THEN
          WRITE(6,*)'ERROR DEALLOCATING COULOMB'
        ENDIF
        DEALLOCATE(RHOG,STAT=IERR)
        IF(IERR.NE.0)THEN
          WRITE(6,*)'ERROR DEALLOCATING RHOG'
        ENDIF
%endif
       END IF

!
! READ AVERAGE
!
        AVG=0.15D0
        AVERAGE=.TRUE.
        OPEN(99,FILE='AVRGDAT',FORM='FORMATTED',STATUS='UNKNOWN')
        REWIND 99
        READ(99,*,END=30)AVG,AVERAGE
   30   CONTINUE
        REWIND(99)
        WRITE(99,*)AVG,AVERAGE,' AVG, AVERAGE'
        CLOSE(99)
!
! POTENTIAL MIXING. IGNORE START POTENTIAL IF (ITER.EQ.0)
!
       NSPTS=NMSH*NSPN
       IF(AVERAGE)THEN
!
! PERFORM POTENTIAL MIXING
!
        OPEN(99,FILE='POTOLD',FORM='UNFORMATTED',STATUS='UNKNOWN')
        REWIND(99)
        ITER=0
        READ(99,END=500)MPTS,MSPN,ITER
        IF(ITER.EQ.0)GOTO 500
        if (old_mode) call check_inputs
        IF((ITER.EQ.MAXSCF).OR.(NONSCF1))GOTO 500
        IF((MPTS.NE.NMSH).OR.(MSPN.NE.NSPN))THEN
         write(6,*)'APOTNL: FILE POTOLD IS UNUSABLE'
         CALL STOPIT
        END IF
        READ(99)(POTIN(IPTS), IPTS=1,NMSH*NSPN)
        IF(MIXING1.EQ.0) THEN
         IF(AVG.GT.0.0D0)THEN
          PRINT '(A)','BROYDEN MIXING OF POTENTIAL'
          CALL MIXING(1,ITER,AVG,NSPTS,NAMES)
         ELSE
          PRINT '(A)','SIMPLE LINEAR MIXING OF POTENTIAL'
          DO IPTS=1,NSPTS
           POTIN(IPTS)=(1.0D0+AVG)*POTIN(IPTS)-AVG*POT(IPTS)
          END DO  
         END IF
        ENDIF
        DO 200 IPTS=1,NSPTS
         POT(IPTS)=POTIN(IPTS)
  200   CONTINUE
  500   ITER=ITER+1
        REWIND(99)  
        WRITE(99)NMSH,NSPN,ITER
        WRITE(99)(POT(IPTS), IPTS=1,NMSH*NSPN)
        CLOSE(99)
       ELSE
        ITER=0
        OPEN(99,FILE='POTOLD',FORM='UNFORMATTED',STATUS='UNKNOWN')
        REWIND(99)
        WRITE(99)NMSH,NSPN,ITER
        WRITE(99)(POT(IPTS), IPTS=1,NMSH*NSPN)
        CLOSE(99)
       END IF

!
! TIMINGS
!
!       CALL GTTIME(APT2)
!       CALL TIMOUT('COMPLETE APOTNL EXECUTION:          ',APT2-APT1) 
       RETURN
       END SUBROUTINE CALL_FORCES

